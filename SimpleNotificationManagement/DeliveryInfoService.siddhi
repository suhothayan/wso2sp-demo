@App:name("DeliveryInfoService")
@App:description("An external mock service that gives the expected item delivery date.")

@source(type='http-request', source.id='delivery-info', receiver.url='http://localhost:8080/delivery-info/',
    @map(type='json', @attributes(messageId='trp:messageId', shipmentType='$.event.shipmentType')))
define stream DeliveryInfoService (messageId string, shipmentType string);

@sink(type='http-response', source.id='delivery-info', message.id='{{messageId}}', headers="'content-type:json','content-length:94'",
    @map(type='json', @payload(""" { "event": { "deliveryDate":"{{deliveryDate}}"} } """)))
@sink(type='log')
define stream DeliveryInfoServiceResponce (messageId string, deliveryDate string);

@info(name='ProcessShipment')
from DeliveryInfoService
select messageId,
    ifThenElse(shipmentType == 'DHL',
    time:dateAdd( time:currentDate(), 2,'DAY',"yyyy-MM-dd"),
    time:dateAdd( time:currentDate(), 7,'DAY',"yyyy-MM-dd")) as deliveryDate
insert into DeliveryInfoServiceResponce;